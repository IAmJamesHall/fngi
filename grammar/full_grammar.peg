# A file is a list of let statements
file = ( (ws / comment / let) ws? ";"? )*

# Lexemes
ws = ~"\\s+"          # whitespace is ignored
numeric = ~"[0-9]"    # note: ~"..." is a regex
alpha  = ~"[A-Z]"i    # note: i means "ignore case"
alphanumeric = ~"[0-9A-Z]"i
never = ws? "[" "]"  # never/void type and value
rawStr = ws? ~'"[^"]"'s               # "this is a raw string"
escapedStr = ws? ~'(?s)\\\".*?\\\"'s  # \"this is an escaped string\"


# Comments
comment = lineComment / blockComment
lineComment = ws? ~"//.*\n"       # // a line comment
blockComment = ws? ~"/\\*.*?\\*/"s  # /* a block comment */


number = ws? numeric+
iden   = ws? alpha alphanumeric*
operator =  (
  "$"
  / "==" / "!=" / "and" / "or"
  / "bitand" / "bitor" / "bitxor"
  / "+" / "-" / "*" / "/"
)

unaryOperator = (
  "&" / "@" / "not" / "-"
)


# Blocks, expressions and statements
expression = ws? expressionUnit ( ws? operator expressionUnit )*
block = ws? "(" statement* expression? ws? ")"
statement = ( let / expression ) ws? ";"

let = ws? "let" iden ws? ":" type ws? "=" expression
expressionUnit = ws? unaryOperator? ( value / name / block )


# Names: A macro call can be anywhere a name can appear
name = singleMacro / doubleMacro / nameNoMacro

singleMacro = ws? "!" nameNoMacro expression
doubleMacro = ws? "!!" nameNoMacro expression expression

# A non-macro name is composed of name units chained by "."
nameNoMacro = nameUnit (ws? "." nameUnit)*
nameUnit = ws? iden nameBlock?
nameBlock = (
  ws? "[" 
    number? ws? "&"? name 
    (ws? ";" number? ws? "&"? name)* ws? ";"? ws? 
  "]")


# Types
type = ws? "&"? ( typeFn / typeNoFn )
typeFn = typeNoFn ws? "->" typeNoFn
typeNoFn = ws? (
  never / stack / array
  / structTyBlock / paramsTyBlock
  / name
)

idenType = iden ws? ":" type
structTyBlock = ws? "[" idenType (ws? ";" idenType)* ws? ";"? ws? "]"
paramsTyBlock = ws? "[" idenType (ws? "," idenType)* ws? ","? ws? "]"

stack = ws? "stk" ws? "[" type (ws? "," type)* ws? ","? ws? "]"
array = ws? "[" number type ws? "]"

# Values
value = ws? (
  never / number / rawStr / escapedStr
  / structValue / paramsValue
)

idenEq = iden ws? "=" expression
structValue = ws? "{" idenEq ( ws? ";" idenEq )* ws? ";"? ws? "}"
paramsValue = ws? "{" expression ( ws? "," expression )* ws? ","? ws? "}"
