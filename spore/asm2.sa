// This file creates some essential assembly utilities including

@vHeap .4 FT^ =h2  // h2: {val:2} push 2bytes from stack to heap
  .4 FTML SWP; .2@vHeap,  // fetch heap and swap {heap, val}
  .2      SR;             // store 2 byte value at heap
  .4 FTML INC2; .2@vHeap, // fetch heap and increment by 2
  .4 SRML  RET; .2@vHeap, // store new heap and return

@vHeap .4 FT^ =h4  // h4: {val:4} push 4bytes from stack to heap
  .4 FTML SWP; .2@vHeap,  // ft heap
  .4      SR;             // sr 4byte value
  .4 FTML INC4; .2@vHeap, // ft heap and +4
  .4 SRML  RET; .2@vHeap, // sr new heap

// {v:4} compile a LIT4 (value-only) to the heap for LIT4.
// Note: compiled as two U2 values with largest first.
@vHeap .4 FT^ =hL4
  // Duplicate, then call h2(v>>0x10)
  DUP;   LIT SHR XSL; #10 $h2  @h2 $h2 // Compile high bits
  JMPL; @h2 $h2

@vHeap .4 FT^ =dictSet // dctSet: Set "standard" dictionary to next token.
  .4 LIT DVF; @D_scan $h2
  .4 LIT4; @c_vDictBuf $hL4  FT; // dict.buf
  .4 LIT4; @c_vDictHeap $hL4     // &dict.heap
  .4 LIT DVS RET; .2@D_dict,

// Define $loc for cleaner function/etc names.
.4@vHeap FT^ =loc // fetch current heap and set to loc
  FTML JMPL; @vHeap $h2 @dictSet $h2

$loc dictGet / { -> v}
  .4 LIT DVF    ; .2@D_scan,
  .4 LIT4; @c_vDictBuf $hL4  FT; // dict.buf
  .4 LIT4; @c_vDictHeap $hL4     // &dict.heap
  .4 LIT DVF RET; .2@D_dict,

.4
$loc getHeap     FTML RET; @vHeap $h2
$loc setHeap     SRML RET; @vHeap $h2
$loc getTopHeap  FTML RET; @vTopHeap $h2
$loc setTopHeap  SRML RET; @vTopHeap $h2
$loc getSz       LIT DVF RET; @D_sz $h2
$loc setSz       LIT DVS RET; @D_sz $h2
$loc getWsLen    LIT DVF RET; @D_wslen $h2
$loc c_xsCatch   LIT DVF RET; @D_xsCatch $h2
$loc c_scan      LIT DVF RET; @D_scan $h2
$loc c_asemble   LIT DVS RET; @D_comp $h2
$loc c_assembleNext // scan and assemble
  LIT DVF; @D_scan $h2
  LIT DVS RET; @D_comp $h2
$loc dForget LIT DVF; @D_scan $h2 LIT DVS RET; @D_rdict $h2 // $dForget f

.4 $loc c_maskInstr
  LIT DVS RET; @D_instr $h2 // use mask on instr

.4 $loc c_compMaskInstr // {maskInstr:4}
  LIT DVS; @D_instr $h2 // use mask on instr
  LIT DVF; @D_instr $h2 // get instr
  XSL; @h2 $h2 // compile instr to heap

  // Clear current instr (preserves sz)
  LIT4; @CLR_INSTR $hL4
  LIT DVS RET; @D_instr $h2

// **********
// * Core macros:
// These are used for executing code with combined instructions and literals,
// as well as setting up forward jumps.
//
// These are combined with the current instruction, using whatever size it is.
//   Ex: `ADD $xsl foo`         -> `ADD XSL; @foo $h2`
// The l2_* variants also include a 2byte literal:
//   Ex: `ADD #321 $l2_xsl foo` -> `LIT ADD XSL; #321 $h2 @foo $h2`
//
// Additionally, `$L` or `$retL` can be used to only push literals. They are
// smart and use LIT or LIT4 depending on the size of the value. Note: they
// ignore current instr since LIT4 is an operation.
//   Ex: $L@myVar   $L#1234_5678   $retL#3

.4 $loc xsl // compile instr with XSL, see docs on core macros.
  LIT4 XSL; @XSL $hL4  @c_compMaskInstr $h2 // compile with XSL
  XSL; @dictGet $h2 // get next token's ptr
  JMPL; @h2 $h2 // put on stack

.4 $loc jmpl // compile instr with JMPL, see docs on core macros.
  LIT4 XSL; @JMPL $hL4  @c_compMaskInstr $h2 // compile with JMPL
  $xsl dictGet // get next token's ptr
  JMPL; @h2 $h2 // put on stack

.4 $loc l2_xsl // compile instr with a LIT XSL, see docs on core macros.
  LIT4; @LIT $hL4 // get LIT maskInstr
  LIT DVS; @D_instr $h2 // use mask on instr
  LIT4 XSL; @XSL $hL4 @c_compMaskInstr $h2 // compile w/XSL
  $xsl h2 // compile literal
  $xsl dictGet // get next token value
  $jmpl h2 // compile it

.4 $loc l2_jmpl // compile instr with a LIT JMPL, see docs on core macros.
  LIT4 XSL; @LIT $hL4  @c_maskInstr $h2 // mask instr w/LIT
  LIT4 XSL; @JMPL $hL4 @c_compMaskInstr $h2 // compile w/JMPL
  $xsl h2 // compile literal
  $xsl dictGet // get next token value
  $jmpl h2 // compile it


.4 $loc IF
  LIT4 XSL; @JZL $hL4  @c_compMaskInstr $h2 // compile with JZL
  $xsl getHeap // push location for END
  #1BAD $l2_jmpl h2 // compile 1BAD to jmp location

.4 $loc lit_IF
  LIT4 XSL; @JZL $hL4  @c_compMaskInstr $h2 // compile with JZL
  $xsl h2 // compile literal
  $xsl getHeap // push location for END
  #1BAD $l2_jmpl h2 // compile 1BAD to jmp location

.4 $loc END $xsl getHeap   .2 SR RET;

// $LOOP ... $BREAK0 ... $AGAIN $END
$loc LOOP   $jmpl getHeap // push location for AGAIN
$loc BREAK0 $xsl IF   SWP RET;
$loc lit_BREAK0 $xsl lit_IF   SWP RET;
$loc AGAIN
  LIT4 XSL; @JMPL $hL4  @c_compMaskInstr $h2 // compile with JMPL
  $jmpl h2 // compile location to jmp to

// Jump location creation and update
.2 $loc jloc // declare a jump location. ex: $jloc j_to
  $xsl getHeap      $xsl dictSet // set dict of next token to heap
  #BAD $l2_jmpl h2 // set asside space to update
.4 $loc jset // Update jloc with current 2byte heap. ex: $jset j_to
  $xsl dictGet    $xsl getHeap // {addr curHeap}
  .2 SR RET; // store curHeap at addr

.4 $loc L // compile next value as literal
  $xsl c_assembleNext // assemble next token (i.e. @myVal)
  // Check if it is >= #10000 and use LIT4, else LIT
  DUP;  LIT INC; #FFFF $h2  GE_U JZL; $jloc j_2byte
    @Sz4 @LIT4 ADD^  $l2_xsl h2 // compile {.4 LIT4}
    $jmpl hL4 // compile 4byte literal value.
  $jset j_2byte
    @Sz2 @LIT ADD^   $l2_xsl h2 // compile {.2 LIT}
    $jmpl h2 // compile 2byte literal value.
$dForget j_2byte

.4 $loc retL // compile next value as literal and return
  $xsl c_assembleNext // assemble next token (i.e. @myVal)
  // Check if it is >= #10000 and use LIT4, else LIT
  DUP;  LIT INC; #FFFF $h2  GE_U JZL; $jloc j_2byte
    @Sz4 @LIT4 @RET ADD^ADD^  $l2_xsl h2 // compile {.4 LIT4 RET}
    $jmpl hL4 // compile 4byte literal value.
  $jset j_2byte
    @Sz2 @LIT @RET ADD^ADD^   $l2_xsl h2 // compile {.2 LIT RET}
    $jmpl h2 // compile 2byte literal value.
$dForget j_2byte

// **********
// * Utilities

// $X: instantly execute non-small function.
// Ex: $X foo, where foo has local variables.
$loc X $xsl dictGet  XW; RET; 

// Get next token dict reference
$loc c_dRef     $xsl c_scan  .4 LIT DVF RET; @D_rdict $h2

.4 $loc hAlign4 // align heap
  $xsl getHeap  DUP;
  LIT MOD; #4 $h2 // {heap heap%4}
  DUP $IF
    SUB;
    ADD #4 $l2_jmpl setHeap // heap = heap - (heap%4) + 4
  $END  DRP2 RET;

.4
// ex: <some check> #E_myError assert
$loc assertNot     SWP; NOT; SWP;               // fallthrough
$loc assert        LIT DVF RET; @D_assert $h2

$loc assertWsEmpty $xsl getWsLen  $L @E_wsEmpty  $jmpl assertNot
$loc tAssert     @E_test $l2_jmpl assert
$loc tAssertNot  @E_test $l2_jmpl assertNot
$loc tAssertEq   .4 EQ  $jmpl tAssert
$loc tAssertNe   .4 NEQ $jmpl tAssert

$assertWsEmpty
